import com.bmuschko.gradle.docker.tasks.image.*
import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.network.*

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath('com.bmuschko:gradle-docker-plugin:3.2.0')
    }
}

docker {
    url = 'tcp://127.0.0.1:2375'
}

def appFileName = 'configuration-service-0.0.1-SNAPSHOT.jar'
def dockerContainerName = 'spring-config-service'
def dockerNetworkName = 'docker-network'
def dockerImageExposePort = 8888
def dockerImageTag = 'spring-config-service'

task copyFilesIntoDockerContext(type: Copy) {
    dependsOn build
    copy {
        from 'build/libs/' + appFileName
        into 'docker/app/'
    }
    copy {
        from 'cacerts'
        into 'docker/app/security/'
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn copyFilesIntoDockerContext
    destFile = project.file('docker/Dockerfile')
    from 'openjdk:8-jre-alpine'
    volume '/srv/git/ /srv/git/'
    runCommand 'mkdir /opt && mkdir /opt/crew-config-server && mkdir ~/.ssh'
    runCommand 'apk update && apk upgrade && apk add git && apk add openssh'
    runCommand 'ssh-keyscan github.com >> ~/.ssh/known_hosts && ssh-keyscan stash1-tools.swacorp.com >> ~/.ssh/known_hosts && cd ~/.ssh/ && ssh-keygen -f rsa -t rsa -N ""'
    runCommand 'git config --global http.sslVerify false && cd /srv/git && git clone https://stash1-tools.swacorp.com/scm/crew/crewconfigrepository.git'
    copyFile 'app/' + appFileName, 'opt/' + appFileName
    exposePort dockerImageExposePort
    entryPoint 'java', '-jar', 'opt/' + appFileName
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = dockerImageTag
}
task createNetwork(type: DockerCreateNetwork) {
    networkId = dockerNetworkName
}

task removeContainer(type: DockerRemoveContainer) {
    targetContainerId { dockerContainerName }
    force true
    onError { exception ->
        if (!exception.message.contains('No such container')) // ignore exception if container does not exist otherwise throw it
            throw exception
    }
}

task createContainer(type: DockerCreateContainer) {
    dependsOn removeContainer, buildImage
    targetImageId { buildImage.getImageId() }
    portBindings = [dockerImageExposePort + ':' + dockerImageExposePort]
    containerName = dockerContainerName
    network dockerNetworkName
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId { createContainer.getContainerId() }
}